--- 
layout: post 
title: A note on these models of SF rent
---

Everyone ([I](http://observer.com/2016/05/a-guy-just-transcribed-30-years-of-for-rent-ads/), [II](http://sf.curbed.com/2016/5/17/11692132/rent-data-eric-fischer), [III](http://www.fastcoexist.com/3060058/world-changing-ideas/a-reality-check-that-explains-san-franciscos-housing-crisis), [IV](https://www.washingtonpost.com/news/wonk/wp/2016/05/22/what-it-would-actually-take-to-reduce-rents-in-americas-most-expensive-city/), [V](http://gawker.com/there-is-no-feasible-wage-growth-for-low-income-labor-1778180896), [VI](http://cityobservatory.org/the-long-road-to-san-francisco/)) has been freaking out over Eric Ficher's [remarkable compilation of historical San Francisco rent data](https://experimental-geography.blogspot.com/2016/05/employment-construction-and-cost-of-san.html). The superlatives are deserved: this is extremely interesting data, and gathering it is not a task that anyone else wants to do. So, bravo to Eric. We all owe him one.

What concerns me is the response to Fischer's analysis of the data, which has been worryingly uncritical. What follows here is a quick rundown of what I see as the biggest problem with his (and the policy media's) reading of the data. My goal here is not to produce a "takedown" or something -- I myself only have undergrad-level training in econometrics, and Eric is fair in qualifying his findings -- but instead to encourage some caution in our collective interpretation of these results. Stata code is included at the bottom of the post for reproducibility. 

My main gripe comes down to this: Eric ([and others](http://moreuseful.blogspot.com/2016/05/another-look-at-factors-driving-rents.html)) are running time series regressions using non-stationary data. For the statistically uninitiated, that more or less means that the average of these series changes over time. For example: GDP from 1900-present is non-stationary; GDP *growth* over that time period is stationary. Similarly, there's a constant upward trend in the variables Eric uses:


Formal statistical tests confirm the visual suspicion here. Expressed in levels, these variables are non-stationary. In changes -- first differences and growth rates -- they're fine:


And lastly I'll note that even with these improved models, you *still* have identification problems inherent to estimating supply and demand functions! You can't really use this type of analysis for useful inference. [That's why we have whole academic literatures that cover this stuff](https://twitter.com/DanielKayHertz/status/736265420429271040), written by people who have spent their entire professional lives doing so. So next time, let's pause and think before we impulsively retweet -- or, #aggregate the #content, if that's our line of work -- some internet chatter that reduces an almost unfathomably complex situation into a few questionable regressions.

```
// BASIC CLEANING //

import delimited combined, clear
foreach v of varlist median_rent-cpi {
	replace `v' = "." if `v' == "NA"
	destring `v', replace
}

keep if year >= 1948 & year <= 2014

// GENERATE DERIVED VARIABLES //

tsset year

gen rent = median_rent / cpi
gen wages = total_wages / cpi
gen avg_wage = wages / employment 

local transform_vars rent wages avg_wage housing_units employment 
foreach v in `transform_vars' {
	gen ln_`v' = ln(`v')
	gen d_`v' = d.`v'
	gen g_`v' = d.ln_`v'
}

// SCATTERPLOTS //

graph drop _all
local plot_vars rent housing_units wages employment 
foreach v in `plot_vars' {
	line `v' year, title("`v'") name("`v'")
}
graph combine `plot_vars', xcom title("Monetary values in 1984 dollars")
			   
// STATIONARITY TESTS //

matrix DF = J(4, 4, .)
matrix rownames DF = level log diff growth
matrix colname DF = rent housing_units wage employment 

local test_vars rent housing_units wages employment
local col_index = 1
foreach v in `test_vars' {
	local row_index = 1
	foreach i in "" "ln_" "d_" "g_" {
		qui dfuller `i'`v'
		matrix DF[`row_index', `col_index'] = r(p)
		local ++row_index
	}
	local ++col_index
}
matrix list DF, format(%9.3f)
```





